﻿/*
	Model for a simplified football match.
	Approach is high level, looking at teamA vs teamB, not looking at individual footballers.

	Some assumptions / simplified rules for our football match:

	1. First team to score wins the entire match
	2. teamA kicks off the match
	3. Ball does not go out of bounds
	4. No fouls committed
	5. Shots are not blocked by defenders. It is always the goalkeeper that tries to save the shot.
	6. Players will never pass or dribble backwards
	7. All players are identical
*/

#import "PAT.Lib.Football";

#define teamA 1;
#define teamB 2;

// simplified football pitch, only 4 zones. A large value would mean closer proximity to teamB's goalpost.

#define teamA_penalty_box 20;	// teamA's goalpost is inside this zone
#define teamA_midfield_zone 40;
#define teamB_midfield_zone 60;
#define teamB_penalty_box 80;	// teamB's goalpost is inside this zone

var winner = -1;	// no winner yet at the start

#define teamA_win winner == teamA;
#define teamB_win winner == teamB;

var ball_loc = teamA_midfield_zone;	// initial ball location is team A midfield zone, because teamA starts the match

Attack(team, other) = case {
	team == teamA: AttackA(team, other)
	default: AttackB(team, other)
};

Tackle(team, other) = case {
	team == teamA: TackleA(team, other)
	default: TackleB(team, other)
};

Intercept(team, other) = case {
	team == teamA: InterceptA(team, other)
	default: InterceptB(team, other)
};

Save(team, other) = case {
	team == teamA: SaveA(team, other)
	default: SaveB(team, other)
};

/*
	Three types of defenses. All of them can fail to materialise.
	
	Tackle a dribbling opponent
	Intercept a pass
	Goalkeeper save a shot

*/
TackleDefault(team, other) = tackle -> pcase {
	call(successRate, team, ball_loc): succeed -> Attack(team, other)
	100 - call(successRate, team, ball_loc): fail{ball_loc = call(ball_move_forward, other, 1, ball_loc)} -> Attack(other, team)
};

InterceptDefault(team, other) = intercept -> pcase {
	call(successRate, team, ball_loc): succeed -> Attack(team, other)
	100 - call(successRate, team, ball_loc): fail{ball_loc = call(ball_move_forward, other, 2, ball_loc)} -> Attack(other, team)
	// ball move forward 1 zone for attacking team
};

SaveDefault(team, other) = save -> pcase {
	// logic: large ball_loc means ball near teamB's goalpost. chances are, teamB goalkeeper fail to save the close range shot.
	100 - call(successRate, team, ball_loc): succeed -> Attack(team, other)
	call(successRate, team, ball_loc): fail { winner = other } -> Skip	// teamA scores and hence wins the match!
	// team scores and hence wins the match!
};

AttackDefault(team, other) = pcase {
	5: stay -> Attack(team, other)
	15: run -> Attack(team, other)
	20: dribble -> Tackle(other, team)
	40: pass -> Intercept(other, team)
	20: shoot -> Save(other, team)
};

AttackLongPass(team, other) = pcase {
	10: dribble -> Tackle(other, team)
	60: pass -> Intercept(other, team)
	30: shoot -> Save(other, team)
};

AttackOnlyShoot(team, other) = pcase {
	100: shoot -> Save(other, team)
};

AttackPassShoot(team, other) = pcase {
	50: pass -> Intercept(other, team)
	100: shoot -> Save(other, team)
};

// Team Configuration

AttackA(team, other) = AttackOnlyShoot(team, other);
TackleA(team, other) = TackleDefault(team, other);
InterceptA(team, other) = InterceptDefault(team, other);
SaveA(team, other) = SaveDefault(team, other);

AttackB(team, other) = AttackDefault(team, other);
TackleB(team, other) = TackleDefault(team, other);
InterceptB(team, other) = InterceptDefault(team, other);
SaveB(team, other) = SaveDefault(team, other);

teamA_start = {ball_loc = teamA_midfield_zone} -> Attack(teamA, teamB);
teamB_start = {ball_loc = teamB_midfield_zone} -> Attack(teamB, teamA);

#assert teamA_start reaches teamA_win with prob;
#assert teamB_start reaches teamB_win with prob;

coin_toss = pcase {
	50: teamA_start
	50: teamB_start
};

#assert coin_toss reaches teamA_win with prob;	// prob of teamA winning the match

/* 
	Probability of teamA winning the match is 55.63%
	
	That is to say, teamA has a greater chance of winning the match compared to teamB! (44.37%)
	
	Why is that so?
	Observe the match conditions - teamA gets to start the match, at teamA_midfield_zone.
	Apparently, that gives teamA a slight advantage.
*/
