/*
	Model for a simplified football match.
	Approach is high level, looking at teamA vs teamB, not looking at individual footballers.
*/

#define teamA 1;
#define teamB 2;

// simplified football pitch, only 4 zones.

#define teamA_penalty_box 20;	// teamA's goalpost is inside this zone
#define teamA_midfield_zone 40;
#define teamB_midfield_zone 60;
#define teamB_penalty_box 80;	// teamB's goalpost is inside this zone

// In other words a larger value would mean closer proximity to teamB's goalpost.

var winner = -1;	// no winner yet at the start

#define teamA_win winner == teamA;
#define teamB_win winner == teamB;

var ball_loc = teamA_midfield_zone;	// initial ball location is team A midfield zone, because teamA starts the match


teamA_attack = pcase {
	5: stay -> teamA_attack
	15: run -> teamA_attack
	20: dribble -> teamB_tackle
	40: pass -> teamB_intercept
	20: shoot -> teamB_goalkeeper_save
};

teamB_attack = pcase {
	5: stay -> teamB_attack
	15: run -> teamB_attack
	20: dribble -> teamA_tackle
	40: pass -> teamA_intercept
	20: shoot -> teamA_goalkeeper_save
};


/*
	Three types of defenses. All of them can fail to materialise.
	
	Tackle a dribbling opponent
	Intercept a pass
	Goalkeeper save a shot

*/

teamB_tackle = pcase {

	// logic: large ball_loc means ball in teamB's defense zone, more teamB defenders to tackle

	ball_loc: succeed -> teamB_attack
	100 - ball_loc: fail {
			
			if (ball_loc != teamB_penalty_box) {
				ball_loc = ball_loc + 20;	// teamA increment by one zone	
			}
			
		} -> teamA_attack
};

teamB_intercept = pcase {
	
	// logic: large ball_loc means ball in teamB's defense zone, more teamB defenders to intercept

	ball_loc: succeed -> teamB_attack
	100 - ball_loc: fail {
			
			if (ball_loc != teamB_penalty_box && ball_loc != teamB_midfield_zone) {
				ball_loc = ball_loc + 40;	// teamA increment by two zones
			}
			
		} -> teamA_attack
};

teamB_goalkeeper_save = pcase {

	// logic: large ball_loc means ball near teamB's goalpost. chances are, teamB goalkeeper fail to save the close range shot.
	
	100 - ball_loc: succeed -> teamB_attack
	ball_loc: fail { winner = teamA } -> Skip	// teamA scores and hence wins the match!
};


teamA_tackle = pcase {

	// logic: small ball_loc means ball in teamA's defense zone, more teamA defenders to tackle

	100 - ball_loc: succeed -> teamA_attack
	ball_loc: fail {
			
			if (ball_loc != teamA_penalty_box) {
				ball_loc = ball_loc - 20;	// teamB "increment" by one zone
			}
			
		} -> teamB_attack
};

teamA_intercept = pcase {
	
	// logic: small ball_loc means ball in teamA's defense zone, more teamA defenders to intercept

	100 - ball_loc: succeed -> teamA_attack
	ball_loc: fail {
			
			if (ball_loc != teamA_penalty_box && ball_loc != teamA_midfield_zone) {
				ball_loc = ball_loc - 40;	// teamB "increment" by two zones
			}
			
		} -> teamB_attack
};

teamA_goalkeeper_save = pcase {

	// logic: small ball_loc means ball near teamA's goalpost. chances are, teamA goalkeeper fail to save the close range shot.
	
	ball_loc: succeed -> teamA_attack
	100 - ball_loc: fail { winner = teamB } -> Skip	// teamB scores and hence wins the match!
};

match = teamA_attack;	// teamA starts the match

#assert match reaches teamA_win with prob;	// prob of teamA winning the match

/* 
	Probability of teamA winning the match is 55.89%
	
	That is to say, teamA has a greater chance of winning the match compared to teamB! (44.11%)
	
	Why is that so?
	Observe the match conditions, teamA gets to start the match, at teamA_midfield_zone.
	Apparently, that gives teamA a slight advantage.
*/
